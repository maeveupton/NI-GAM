model {

  for (i in 1:n) {
   # Main likelihood
    y[i] ~ dnorm(mu[i], sigmasq_all[i]^-1)
    sigmasq_all[i] <- sigma_res^2 + sigma_known[i]^2 

    # Mean structure
    mu[i] <- r[i] + g_z_x[i] + h_z_x[i]
    
    # Site specific vertical offset 
    h_z_x[i] <- intercept[site[i]]

    # Linear Local component
    g_z_x[i] <- age[i] * b_g[site[i]]
    
    # Linear Local component + Site specific vertical offset 
    g_h_z_x[i] <- h_z_x[i] + g_z_x[i]
    
    # Pulling out residuals of model
    residuals_mod1[i] <- y[i] - mu[i]
  }

  # Regional term
  r <- B_r %*% b_r
  r_grid <- B_r_grid %*% b_r
  for (j in 1:n_knots_r) {
    b_r[j] ~ dnorm(0, sigma_r^-2)
  }

  # Prior on intercept
  for (j in 1:n_sites) {
    intercept[j] ~ dnorm(0, sigma_h^-2)
  }

  # Linear Local component
  for (j in 1:n_sites) {
    b_g[j] ~ dnorm(known_rate[j], known_rate_err[j]^-2)
  }

  # Priors
  sigma_r~ dt(0, 1^-2, 1)T(0,)
  sigma_h ~ dt(2.5, 2^-2, 1)T(0,)
  sigma_res ~ dt(0, 1^-2, 1)T(0,)
  

}
