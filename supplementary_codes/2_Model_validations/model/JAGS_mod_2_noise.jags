model {

  for (i in 1:n) {
    # Main likelihood
    y[i] ~ dnorm(mu[i], sigmasq_all[i]^-1)
    sigmasq_all[i] <- sigma_res^2 + sigma_known[i]^2 + extra[i]^2

    # Mean structure
    mu[i] <- y_offset[i] + GIA_correction[i] + local[i]+ regional[i]
  
    # y offset term
    y_offset[i] <- intercept[site[i]]
    
    # GIA correction term
    GIA_correction[i] <- age[i] * b_GIA[site[i]]
    
    # GIA + y offset
    GIA_yoffset[i] <- y_offset[i] + GIA_correction[i]
    
    # Pulling out residuals of model
    residuals[i] <- y[i] - mu[i]
  }

  # Regional term
  regional <- B_regional %*% b_regional
  regional_pred <- B_regional_pred %*% b_regional
  
  for (j in 1:n_knots_regional) {
    b_regional[j] ~ dnorm(b_r_value[j], b_r_sd_value[j]^-2)
  }

  # Local term
  local <- B_local %*% b_local
  local_pred <- B_local_pred %*% b_local
  for (j in 1:n_knots_local) {
    b_local[j] ~ dnorm(0, sigma_local^-2)
  }

  # Prior on yoffset
  for (j in 1:n_sites) {
    intercept[j] ~ dnorm(y0_value[j], y0_sd_value[j]^-2)
  }

  # Prior on GIA_correction
  for (j in 1:n_sites) {
    b_GIA[j] ~ dnorm(GIA_slope[j], GIA_slope_sd[j]^-2)
  }

  # Prior for sigmas - informative
  sigma_local ~ dt(0, 1^-2, 1)T(0,) 
  sigma_res ~ dt(0, 1^-2, 1)T(0,)
  
  # Loop for predictions
  for(i in 1:n_pred){
    y_offset_pred[i] <- intercept[site_pred[i]]
    GIA_correction_pred[i] <- age_pred[i] * b_GIA[site_pred[i]]
    mu_pred[i] <-  y_offset_pred[i] + GIA_correction_pred[i] + regional_pred[i] + local_pred[i]
    sigmasq_all_pred[i] <- sigma_res^2 + sigma_known_pred[i]^2 + extra_pred[i]^2
    y_pred[i] ~ dnorm(mu_pred[i], sigmasq_all_pred[i]^-1)
    # Pulling out residuals of model
    residuals_pred[i] <- y_pred[i] - mu_pred[i]
  }
  
}
