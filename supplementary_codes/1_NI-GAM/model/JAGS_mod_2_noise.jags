model {

  for (i in 1:n) {
    # Main likelihood
    y[i] ~ dnorm(mu[i], sigmasq_all[i]^-1)
    sigmasq_all[i] <- sigma_res^2 + sigma_known[i]^2 + extra[i]^2

    # Mean structure
    mu[i] <- y_offset[i] + GIA_correction[i] + local[i]+ regional[i]
  
    # y offset term
    y_offset[i] <- intercept[site[i]]
    
    # GIA correction term
    GIA_correction[i] <- age[i] * b_GIA[site[i]]
    
    # GIA + y offset
    GIA_yoffset[i] <- y_offset[i] + GIA_correction[i]
  }

  # Regional term
  regional <- B_regional %*% b_regional
  # Regional term grid 
  regional_grid <- B_regional_grid %*% b_regional
  
  for (j in 1:n_knots_regional) {
    #b_regional[j] ~ dnorm(b_value, sigma_regional^-2)
    b_regional[j] ~ dnorm(b_r_value[j], b_r_sd_value[j]^-2)
  }
  

  

  # Local term
  local <- B_local %*% b_local
  for (j in 1:n_knots_local) {
    b_local[j] ~ dnorm(0, sigma_local^-2)
  }

  # Prior on yoffset
  for (j in 1:n_sites) {
    intercept[j] ~ dnorm(y0_value[j], y0_sd_value[j]^-2)
    #intercept[j] ~ dnorm(0, sigma_site^-2)
  }


  # Prior on GIA_correction
  for (j in 1:n_sites) {
    b_GIA[j] ~ dnorm(GIA_slope[j], GIA_slope_sd[j]^-2)
  }

  # Prior for sigmas - informative
  # Parameters are (mean/sd)^2, mean/(sd^2) respectively
  
  # Fix local
  #sigma_regional <- sigma_r_value
  #sigma_regional ~ dt(sigma_r_value,sigma_r_sd_value^-2,1)T(0,)
  sigma_local ~ dt(0, 1^-2, 1)T(0,) 
  sigma_site ~ dt(0, 2^-2, 1)T(0,)
  sigma_res ~ dt(0, 1^-2, 1)T(0,)

}
